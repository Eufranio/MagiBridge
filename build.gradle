plugins {
    id 'org.spongepowered.plugin' version '0.9.0'
    id 'com.github.johnrengelman.shadow' version '4.0.4'
    id 'com.qixalite.spongestart2' version '4.0.0'
}

group = pluginGroup
version = pluginVersion

dependencies {
    shadow 'org.spongepowered:spongeapi:7.4.0'
    //shadow 'io.github.nucleuspowered:nucleus-api:1.8.0-S7.1'
    shadow 'br.net.fabiozumbi12.UltimateChat:UltimateChat-Sponge-56:1.9.1'
    shadow files('libs/boop.jar')
    shadow files('libs/mcclans.jar')
    shadow 'com.github.Arckenver:Nations:v2.11_api7'
    shadow 'org.apache.logging.log4j:log4j-core:2.17.2'
    shadow 'com.github.Aquerr:EagleFactionsAPI:0.15.0'
    shadow 'com.github.rojo8399:PlaceholderAPI:master-SNAPSHOT'

    compile 'com.mashape.unirest:unirest-java:1.4.9'
    compile ('net.dv8tion:JDA:4.2.1_253') {
        exclude module: 'opus-java'
    }
    compile 'com.vdurmont:emoji-java:4.0.0'

    compile project(':nucleus-v1')
    compile project(':nucleus-v2')
}

spongestart {
    minecraft '1.12.2'
}

repositories {
    mavenCentral()
    jcenter()
    maven {
        name 'm2-dv8tion'
        url 'https://m2.dv8tion.net/releases'
    }
    maven { url = "https://raw.github.com/FabioZumbi12/UltimateChat/mvn-repo/" }
    maven { url 'https://jitpack.io' }
}

shadowJar {
    relocate('org.apache', 'magibridge.org.apache') {
        exclude 'org/apache/logging/**'
    }
    relocate 'org.json', 'magibridge.org.json'
    relocate 'gnu.trove', 'magibridge.gnu.trove'
    relocate 'com.mashape.unirest', 'magibridge.com.mashape.unirest'
    relocate 'com.squareup', 'magibridge.com.squareup'
    relocate 'okio', 'magibridge.okio'
    relocate 'okhttp3', 'magibridge.okhttp3'
    relocate 'net.dv8tion', 'magibridge.net.dv8tion'
    relocate 'com.neovisionaries.ws.client', 'magibridge.com.neovisionaries.ws.client'

    relocate 'com.fasterxml.jackson', 'magibridge.com.fasterxml.jackson'
    dependencies {
        exclude(dependency('org.slf4j:slf4j-api'))
        exclude(dependency('net.java.dev.jna:jna'))
    }
    exclude('module-info.class')
    baseName = 'MagiBridge-API7'
    classifier = null
}

build.dependsOn shadowJar

sponge.plugin.id = pluginId
compileJava.options.encoding = 'UTF-8'
